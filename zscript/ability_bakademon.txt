class bakademonabilities:HDMagicBase{
	default{
		hdweapon.refid "cac";
		obituary "$OB_BAKADEMONPLAYER";
		tag "$TAG_BAKADEMONPLAYER";
	}
	int charge;
	bool itsbigzaptime;

	override string gethelptext(){
		return
		WEPHELP_FIRE.."  Conjure Foof\n"
		..WEPHELP_ALTFIRE.."  Reel in Target (If any)\n"
		..WEPHELP_ZOOM.." + "..WEPHELP_FIRE.."  Major Zap Attack\n"
		..WEPHELP_ZOOM.." + "..WEPHELP_ALTFIRE.."  Minor Zap Attack\n
		Charges in store: "..charge
		;
	}

	override string,double getpickupsprite(){
		return "BAL2A0",1.;
	}

	states{
	fire:
		TNT1 A 0 A_JumpIf(pressingzoom(),"kekb");
		TNT1 A 0 A_JumpIfInventory("HDGreens",25,"foof");
		goto notenoughgreens;

	altfire:
		TNT1 A 0 A_JumpIf(pressingzoom(),"foofighter");
		TNT1 A 0 A_JumpIfInventory("HDGreens",36,"reel");
		goto notenoughgreens;

	kekb:
		TNT1 A 0{
			if(invoker.charge<30){setweaponstate("nocharge");return;}
			if(!countinv("HDGreens")>=80)return;
			invoker.itsbigzaptime=true;
			setweaponstate("bigzap");
		}
		goto notenoughgreens;

	foofighter:
		TNT1 A 0{
			if(invoker.charge<30){setweaponstate("nocharge");return;}
		}
		TNT1 A 0 A_JumpIfInventory("HDGreens",40,"bigzap");
		goto notenoughgreens;

	foof:
		TNT1 B 2;
		TNT1 C 1;
		TNT1 D 6 bright{
			A_FireProjectile("PlayerFoof");
			let hdp=hdplayerpawn(self);
			hdp.fatigue+=3;
			A_GunFlash();
			A_TakeInventory("HDGreens",25);
			invoker.charge++;
			A_SetHelpText();
		}
		TNT1 C 2;
		TNT1 B 3;
		TNT1 A 4 A_Refire("fire");
		goto nope;
	bigzap:
		TNT1 B 2;
		TNT1 C 3;
		TNT1 D 0 bright{
			bnopain=true;
			A_FireProjectile("Triloball",spawnheight:6);
			if(invoker.itsbigzaptime){
				A_StartSound("caco/sight",CHAN_VOICE,volume:1.,attenuation:0.1);
			}else A_StartSound("caco/sight",CHAN_VOICE);
		}
		TNT1 DDDDDDDDDDDDDDDDDD 2 bright A_GunFlash();
		TNT1 D 24{
			distantnoise.make(self,"caco/bigexplodefar2");
			A_StartSound("caco/bigshot",CHAN_WEAPON);
			A_ChangeVelocity(-cos(pitch)*3,0,sin(pitch),CVF_RELATIVE);
			if(invoker.itsbigzaptime){
				HDBulletActor.FireBullet(self,"KekB",32);
			A_TakeInventory("HDGreens",80);
			}else{
			A_TakeInventory("HDGreens",40);
				A_CustomRailgun(random(100,200),50,"","azure",
					RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,
					0,40.0,null,0,0,2048,
					12,0.4,2.0,"",-4
				);
				actor bll=LineAttack(
					angle,2048,pitch,random(128,512),"","PlayerFooFighter"
				);
				if(bll){
					hdactor.CacoZapArc(bll,self);
					bll.stamina=0;
						for(int i=0;i<3;i++){
							//bll.tracer=target;
							bll.A_SpawnItemEx("PlayerFooFighter",
								0,0,3,frandom(-1,4),0,frandom(1,5),
								angle+frandom(-50,50),
								SXF_ABSOLUTEANGLE|
								SXF_NOCHECKPOSITION|
								SXF_TRANSFERPOINTERS
							);
						}
				}
			}
			invoker.itsbigzaptime=false;
			invoker.charge-=30;
			let hdp=hdplayerpawn(self);
			hdp.fatigue+=18;
			A_SetHelpText();
			bnopain=false;
		}
		TNT1 C 6;
		TNT1 B 3;
		TNT1 A 6;
		goto nope;
	reel:
		TNT1 BB 2;
		#### C 4 A_StartSound("caco/sight");
		#### D 2 bright A_FireProjectile("Triloball",spawnheight:6);
		#### DDDDDDDDDDDD 2 bright{
			target=null;tracer=null;A_GunFlash();
			if(invoker.charge<3){setweaponstate("nope");return;}
			flinetracedata bruh;
			LineTrace(
				angle,meleerange,pitch,
				TRF_ALLACTORS,
				offsetz:height-6,
				data:bruh
			);
			if(bruh.hitactor){
			target=bruh.hitactor;tracer=bruh.hitactor;
			}
		A_TakeInventory("HDGreens",3);
		actor tgt=tracer;
		if(target){
			double range=meleerange+target.radius*1.14;
			range*=range;
			if(range>distance3dsquared(target)){
				double directness=20-absangle(angleto(target),angle);
				if(directness>0){
					tgt=target;
					hdactor.CacoZapArc(tgt,tgt,hdactor.ARC2_SILENT);

					vector3 pvv=(
						(tgt.pos.xy,tgt.pos.z+tgt.height*0.5)
						-(pos.xy,pos.z+height*0.5)
					).unit();
					vel-=pvv*0.2;
					if(!tgt.bdontthrust){
						if(tgt.pos.z<=tgt.floorz)pvv.z+=3;
						tgt.vel-=pvv*100/max(1,tgt.mass);
						tgt.angle+=frandom(-1,1);
						tgt.pitch+=frandom(-1,1);
					}

					target.damagemobj(tracer,self,int(frandom(1,directness+2)),"electrical");
				}
			}
		}
		hdactor.CacoZapArc(tracer,tgt,hdactor.ARC2_RANDOMDEST,radius:64,height:64);
	}
		#### C 4;
		#### B 2;
		#### A 6;
		goto nope;
	}
}

class PlayerFooFighter:HDActor{
	bool foowizard;
	bool foocleric;
	default{
		+bright +nogravity +float +noblockmap
		+seekermissile +missile
		+puffonactors +bloodlessimpact +alwayspuff +puffgetsowner +hittracer

		+rollsprite +rollcenter
		+forcexybillboard +bright
		renderstyle "add";

		height 20;radius 20;
		speed 20;
		maxstepheight 64;

		damagetype "electrical";

		seesound "caco/ballhum";
	}
	override void beginplay(){
		super.beginplay();
		vel*=frandom(0.4,1.7);
		stamina=random(300,600);
		ChangeTid(FOOF_TID);

		foowizard=randompick(0,0,0,0,1);
		foocleric=randompick(0,0,0,0,0,1);
	}
	override void tick(){
		if(isfrozen()){
			clearinterpolation();
			return;
		}
		if(bnointeraction){
			roll+=10;
			scale*=1.01;
			A_SpawnItemEx("HDGunSmoke",3,0,0,2,0,1,roll,SXF_NOCHECKPOSITION);
			super.tick();
			return;
		}
		roll=frandom(0,360);
		stamina--;

		if(!random(0,32))CacoZapArc(self);

		//apply movement and collision
		speed=vel.xy.length();
		int times=int(max(1,speed/radius));
		vector3 frac=(times==1)?vel:(vel/times);
		fcheckposition tm;
		for(int i=0;i<times;i++){
			if(stamina<1||!trymove(pos.xy+frac.xy,true,true,tm)){
				if(
					stamina>0&&random(0,blockingmobj==null?2:7)
				){
					setorigin((pos.xy+frac.xy,pos.z),true);
					continue;
				}

				//bzz
				if(blockingmobj){
					if(
						target
					)continue;

					int pcbak=blockingmobj.painchance;
					blockingmobj.painchance=max(pcbak,240);
					blockingmobj.DamageMobj(self,target,random(1,3),"electrical");
					blockingmobj.painchance=pcbak;

					A_StartSound("caco/ballcrack",CHAN_WEAPON);
					while(random(0,2))A_SpawnParticle("white",
						SPF_RELATIVE|SPF_FULLBRIGHT,35,frandom(4,8),0,
						frandom(-4,4),frandom(-4,4),frandom(0,4),
						frandom(-1,1),frandom(-1,1),frandom(1,2),
						frandom(-0.1,0.1),frandom(-0.1,0.1),-0.05
					);
					if(random(0,3)){
						setorigin((pos.xy+frac.xy,pos.z),true);
						stamina-=1;
						vel*=0.9;
						continue;
					}
				}

				bmissile=false;
				bnointeraction=true;

				//kaBOOM
				A_HDBlast(
					blastradius:128,blastdamage:128,blastdamagetype:"electrical",
					pushradius:256,pushamount:512,pushmass:true,
					immolateradius:72,immolateamount:random(30,80),immolatechance:40,
					hurtspecies:false
				);
				distantnoise.make(self,"caco/bigexplodefar");
				A_StartSound("caco/bigexplode",CHAN_VOICE);
				A_StartSound("caco/ballecho",CHAN_BODY);
				A_StartSound("caco/bigcrack",5);

				A_SetSize(radius*2,height*1.4);
				if(
					abs(floorz-pos.z)<10
					||abs(ceilingz-(pos.z+height))<10
					||!checkmove(pos.xy,PCM_NOACTORS|PCM_DROPOFF)
				){
					A_SpawnChunks("HugeWallChunk",12,4,12);
					A_SpawnChunks("BigWallChunk",12,4,12);
					A_SpawnChunks("HDSmoke",3,0,2);
				}
				
				DistantQuaker.Quake(self,4,35,512,10);
				vel=(0,0,0.4);
				scale*=2.;
				setstatelabel("death");
				break;
			}
			addz(frac.z);
			if(pos.z<floorz){
				setz(floorz);
				vel.z=0;
			}else if(pos.z+height>ceilingz){
				setz(ceilingz-height);
				vel.z=0;
			}
		}
		vel.x*=frandom(0.9,1.05);
		vel.y*=frandom(0.9,1.05);
		vel.z*=frandom(0.9,1.05);
		if(accuracy>100&&tracer&&checksight(tracer)){
			A_Face(tracer,0,0,FAF_TOP);
			A_ChangeVelocity(cos(pitch),0,-sin(pitch),CVF_RELATIVE);
		}else if(!random(0,50))A_ChangeVelocity(5,0,0.2,CVF_RELATIVE);
		accuracy--;
		if(accuracy<0)accuracy=160;

		NextTic();
	}
	states{
	spawn:
		BAL2 ABABABABAB 1 light("PLAZMABX1");
		BAL2 A 0 A_Jump(24,"castspell");
		loop;
	castspell:
		BAL2 A 0{
			double achange=random(0,3)?frandom(-24,24):frandom(0,360);
			if(!random(0,3))vel.z=frandom(-vel.z*0.3,vel.z);
			vel.xy=rotatevector(vel.xy,achange);

			if(foowizard){
				int warptimes=random(3,7);
				double spdbak=speed;
				speed=100;
				for(int i=0;i<warptimes;i++){
					A_Wander();
				}
				speed=spdbak;
				setz(frandom(floorz,ceilingz-height));
			}
			if(foocleric){
				if(!tracer){
					foocleric=false;
					foowizard=true;
					return;
				}
				actor itt=null;
				actoriterator it=level.createactoriterator(FOOF_TID,"PlayerFooFighter");
				while(itt=it.next()){
					if(
						PlayerFooFighter(itt)
						&&checksight(itt)
					)itt.vel+=itt.vec3to(tracer).unit()*2;
				}
			}
		}goto spawn;
	death:
		BAL2 CDE 3 light("BAKAPOST1");
		BAL2 E 3 light("PLAZMABX2") A_FadeOut(0.3);
		wait;
	}
}

class PlayerFoof:HDFireball{
	default{
		height 12;radius 12;
		gravity 0;
		decal "BulletScratch"; +hittracer
		damagefunction(random(20,40));
		hdfireball.firefatigue int(HDCONST_MAXFIREFATIGUE*0.25);
	}
	void ZapSomething(){
		roll=frandom(0,360);
		A_StartSound("misc/arczap",CHAN_BODY);
		blockthingsiterator it=blockthingsiterator.create(self,72);
		actor tb=target;
		actor zit=null;
		bool didzap=false;
		while(it.next()){
			if(
				it.thing.bshootable
				&&abs(it.thing.pos.z-pos.z)<72
			){
				zit=it.thing;
				if(
					it.thing
					)master=it.thing;
				if(
					it.thing==target
					)continue;
				if(
					zit.health>0
					&&checksight(it.thing)
					&&(
						!tb
						||master
					)
				){
					//A_Face(zit,0,0,flags:FAF_MIDDLE);
					CacoZapArc(self,zit,ARC2_RANDOMDEST);
					zit.damagemobj(self,tb,random(0,7),"electrical");
					didzap=true;
					break;
				}
			}
		}
		if(!zit||zit==tb){pitch=frandom(-90,90);angle=frandom(0,360);}
		if(!didzap)CacoZapArc(self,null,ARC2_SILENT,radius:32,height:32,pvel:vel);
		if(pos.z-floorz<24)vel.z+=0.3;
	}
	states{
	spawn:
		BAL2 A 0 ZapSomething();
		BAL2 AB 2 light("PLAZMABX1") A_Corkscrew();
		loop;
	death:
		BAL2 C 0 A_SprayDecal("CacoScorch",radius*2);
		BAL2 C 0 A_StartSound("misc/fwoosh",5);
		BAL2 CDE 3 light("BAKAPOST1") ZapSomething();
	death2:
		BAL2 E 0 ZapSomething();
		BAL2 E 3 light("PLAZMABX2") A_FadeOut(0.3);
		loop;
	}
}